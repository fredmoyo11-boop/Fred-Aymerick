import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
  id 'com.github.node-gradle.node' version '2.2.4'
  id 'org.openapi.generator' version '6.0.0'
}

repositories {
  mavenCentral()
}

description = "Frontend"

node {
  version = '22.15.0'
  npmVersion = '11.3.0'
  download = true
}

configurations {
  customOpenApiGenerator
}

dependencies {
  customOpenApiGenerator files("$projectDir/openapi-generator-cli-tuple.jar")
}

tasks.register("deleteClient", Delete) {
  delete fileTree(dir: "./src/api/sep_drive", exclude: ".openapi-generator-ignore")
}

tasks.register("generateClient", JavaExec) {
  dependsOn("deleteClient", ":backend:generateOpenApiDocs")

  group = "openapi"
  description = "Generates Angular API client using custom OpenAPI generator JAR"

  classpath = configurations.customOpenApiGenerator
  mainClass.set("org.openapitools.codegen.OpenAPIGenerator")

  args = [
    "generate",
    "-g", "typescript-angular",
    "-i", "$projectDir/../backend/build/openapi.json",
    "-o", "$projectDir/src/api/sep_drive",
    "--additional-properties=withCredentials=true"
  ]




}

